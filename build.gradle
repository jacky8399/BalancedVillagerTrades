plugins {
    id 'java-library'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.jacky8399'
version = '2.0-beta7'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'papermc-repo'
        url = "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = 'https://jitpack.io'}
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation('com.github.MightyPirates.OC-LuaJ:luaj-jse:new-SNAPSHOT') {
        exclude group: 'org.apache.bcel' // LuaJC is not used
    }

    // Tests
    testImplementation 'org.mockito:mockito-inline:4.8.0'
    testImplementation(platform('org.junit:junit-bom:5.9.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

test {
    useJUnitPlatform()
}

shadowJar {
    relocate 'org.bstats', 'com.jacky8399.balancedvillagertrades.bstats'
    relocate 'org.luaj', 'com.jacky8399.balancedvillagertrades.luaj'
}

//assemble.dependsOn reobfJar
jar.dependsOn shadowJar