plugins {
    id 'java-library'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.papermc.paperweight.userdev' version "1.3.7"
}

group = 'com.jacky8399'
version = '2.0-beta6'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'papermc-repo'
        url = "https://papermc.io/repo/repository/maven-public/"
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.19-R0.1-SNAPSHOT")
    compileOnly 'io.papermc.paper:paper-api:1.19-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'org.luaj:luaj-jse:3.0.1'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'org.bstats', 'com.jacky8399.balancedvillagertrades.bstats'
    relocate 'org.luaj', 'com.jacky8399.balancedvillagertrades.luaj'
}

assemble.dependsOn reobfJar
jar.dependsOn shadowJar